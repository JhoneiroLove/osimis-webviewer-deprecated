# Orthanc - A Lightweight, RESTful DICOM Store
# Copyright (C) 2012-2016 Sebastien Jodogne, Medical Physics
# Department, University Hospital of Liege, Belgium
# Copyright (C) 2016 OSIMIS SA
#
# This program is free software: you can redistribute it and/or
# modify it under the terms of the GNU Affero General Public License
# as published by the Free Software Foundation, either version 3 of
# the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# Affero General Public License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this program. If not, see <http://www.gnu.org/licenses/>.

cmake_minimum_required(VERSION 2.8)

project(OsimisWebViewer)

include(WebViewerLibrary/WebViewerLibrary.cmake)

set(STANDALONE_BUILD ON CACHE BOOL "Standalone build (all the resources are embedded, necessary for releases)")
set(JS_CLIENT_PATH "${CMAKE_SOURCE_DIR}/../frontend/build" CACHE STRING "Path of the front-end build folder")
if(EXISTS ${JS_CLIENT_PATH}) # If file exists (not var)
  # Set frontend version based on local build if available
  # when building inside the docker container, the frontend/build folder is already there and we don't want to override it.
  set(JS_FRONTEND_VERSION "LOCAL")
else()
  # Set frontend version based on the commit id for all other builds including the release version
  set(JS_FRONTEND_VERSION ${PRODUCT_VERSION_COMMIT_SHA1_STRING})
endif()

# Generate embedded resources
set(ORTHANC_ROOT ${CMAKE_SOURCE_DIR}/Orthanc) # needed by AutoGeneratedCode.cmake
include(${ORTHANC_ROOT}/Resources/CMake/AutoGeneratedCode.cmake)
if (STANDALONE_BUILD)
  add_definitions(
    -DORTHANC_STANDALONE=1
    )
  set(EMBEDDED_RESOURCES
    WEB_VIEWER  ${JS_CLIENT_PATH}
    )
else()
  add_definitions(
    -DORTHANC_STANDALONE=0
    -DWEB_VIEWER_PATH="${JS_CLIENT_PATH}/"
    )
endif()

EmbedResources(
  ORTHANC_EXPLORER  ${CMAKE_SOURCE_DIR}/Resources/OrthancExplorer.js
  ${EMBEDDED_RESOURCES}
  )

# Create OsimisWebViewer dynamic library
set(VIEWER_PLUGIN_SOURCE_DIR ${CMAKE_SOURCE_DIR}/WebViewerPlugin)
add_library(OsimisWebViewer
  SHARED
  ${AUTOGENERATED_SOURCES}
  ${PLUGIN_SOURCES}
  ${VIEWER_PLUGIN_SOURCE_DIR}/Plugin.cpp
  )

if (STATIC_BUILD)
  add_dependencies(OsimisWebViewer EmbeddedResourcesGenerator)
endif()

add_dependencies(OsimisWebViewer WebViewerLibrary)
target_link_libraries(OsimisWebViewer WebViewerLibrary)

if (${CMAKE_SYSTEM_NAME} STREQUAL "Windows")
   list(APPEND PLUGIN_SOURCES  ${PLUGIN_SOURCES}/resources.rc)
endif()
message("Setting the version of the library to ${PRODUCT_VERSION_SHORT_STRING}")

set_target_properties(OsimisWebViewer PROPERTIES
    VERSION ${PRODUCT_VERSION_SHORT_STRING}
    SOVERSION ${PRODUCT_VERSION_SHORT_STRING})

install(
  TARGETS OsimisWebViewer
  RUNTIME DESTINATION lib                      # Destination for Windows
  LIBRARY DESTINATION share/orthanc/plugins    # Destination for Linux
  )

# Create OsimisWebViewer unit tests
add_executable(UnitTests
  ${GTEST_SOURCES}
  ${JSONCPP_SOURCES}
  ${CMAKE_SOURCE_DIR}/UnitTestsSources/UnitTestsMain.cpp
  )
add_dependencies(UnitTests WebViewerLibrary)
target_link_libraries(UnitTests WebViewerLibrary)
